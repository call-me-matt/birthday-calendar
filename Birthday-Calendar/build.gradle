apply plugin: 'com.android.application'

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support:preference-v14:28.0.0'
    implementation 'com.takisoft.fix:preference-v7:25.0.0.0'
    implementation 'org.sufficientlysecure:html-textview:2.0'
    implementation 'com.pavelsikun:vintage-chroma:1.5'
}

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 34
        versionCode 19
        versionName "2.0.2"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    /*
     * To sign release build, create file gradle.properties in ~/.gradle/ with this content:
     *
     * signingStoreLocation=/home/key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreLocation') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {
        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    flavorDimensions += "version"
    productFlavors {
        create("full") {
            dimension "version"
            applicationId = "org.birthdayadapter"
            buildConfigField "boolean", "FULL_VERSION", "true"
            buildConfigField "boolean", "GOOGLE_PLAY_VERSION", "false"
        }
        create("full_google") {
            dimension "version"
            applicationId = "org.birthdayadapter"
            buildConfigField "boolean", "FULL_VERSION", "true"
            buildConfigField "boolean", "GOOGLE_PLAY_VERSION", "true"
        }
        create("free") {
            dimension "version"
            applicationId = "org.birthdayadapter.free"
            buildConfigField "boolean", "FULL_VERSION", "false"
            buildConfigField "boolean", "GOOGLE_PLAY_VERSION", "true"
        }
    }
    namespace 'org.birthdayadapter'
    lint {
        disable 'MissingTranslation', 'ExtraTranslation'
    }
    buildFeatures {
        buildConfig true
    }
}
